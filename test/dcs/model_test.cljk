(ns dcs.model-test
  (:require [clojure.test :refer [deftest is]]
            [dcs.model :as m]))

(defn reactor-system []
  (-> (m/system)
      (m/add-area (m/area "area-1" {:name "Reactor"}))
      (m/add-tag (m/tag "TIC-101.PV" :ai {:units :degC :range [0 500] :area "area-1"}))
      (m/add-tag (m/tag "TIC-101.OUT" :ao {:range [0.0 100.0] :area "area-1"}))
      (m/add-loop (m/ctrl-loop "TIC-101" {:pv-tag "TIC-101.PV" :output-tag "TIC-101.OUT"
                                          :mode :auto :setpoint 350.0
                                          :tuning {:kp 2.0 :ki 0.1 :kd 0.0}
                                          :output-limits [0.0 100.0]}))
      (m/add-alarm (m/alarm "TIC-101.HI" {:tag "TIC-101.PV" :type :hi :setpoint 420.0
                                          :priority :high :deadband 2.0}))
      (m/add-alarm (m/alarm "TIC-101.HIHI" {:tag "TIC-101.PV" :type :hi-hi :setpoint 460.0
                                            :priority :critical :deadband 2.0}))))

(deftest builder-and-queries
  (let [sys (reactor-system)]
    (is (= 1 (count (m/areas sys))))
    (is (= 2 (count (m/tags sys))))
    (is (= 1 (count (m/loops sys))))
    (is (= 2 (count (m/alarms sys))))
    (is (= :ai (:dcs/kind (m/tag-by-id sys "TIC-101.PV"))))
    (is (= "TIC-101" (:dcs/id (m/loop-by-id sys "TIC-101"))))
    (is (= 2 (count (m/alarms-for-tag sys "TIC-101.PV"))))
    (is (= 1 (count (m/loops-for-tag sys "TIC-101.PV"))))
    (is (= 1 (count (m/loops-for-tag sys "TIC-101.OUT"))))))

(deftest empty-system-defaults
  (let [sys (m/system)]
    (is (empty? (m/areas sys)))
    (is (empty? (m/tags sys)))
    (is (nil? (m/tag-by-id sys "nope")))))

(deftest loop-default-mode
  (is (= :manual (:dcs/mode (m/ctrl-loop "L1" {})))))
