(ns dcs.runner-test
  (:require [clojure.test :refer [deftest is]]
            [dcs.model :as m]
            [dcs.runner :as runner]))

(defn reactor-system []
  (-> (m/system)
      (m/add-tag (m/tag "PV1" :ai {:range [0 500]}))
      (m/add-tag (m/tag "OUT1" :ao {:range [0.0 100.0]}))
      (m/add-loop (m/ctrl-loop "L1" {:pv-tag "PV1" :output-tag "OUT1" :mode :auto
                                     :setpoint 100.0 :tuning {:kp 1.0 :ki 0.0 :kd 0.0}
                                     :output-limits [0.0 100.0]}))
      (m/add-alarm (m/alarm "A1" {:tag "PV1" :type :hi :setpoint 90.0}))))

(deftest invalid-system-short-circuits
  (let [sys (-> (m/system) (m/add-loop (m/ctrl-loop "L1" {:pv-tag "NOPE" :output-tag "ALSO-NOPE"})))
        result (runner/dry-run sys {})]
    (is (false? (:dcs/valid? result)))
    (is (seq (:dcs/problems result)))
    (is (empty? (:dcs/trace result)))))

(deftest dry-run-simulates-fixed-cycles
  (let [result (runner/dry-run (reactor-system)
                                {:n-cycles 3 :dt 1.0 :initial-values {"PV1" 50.0}})]
    (is (true? (:dcs/valid? result)))
    (is (= 3 (count (:dcs/trace result))))
    (is (= 0 (:dcs/cycle (first (:dcs/trace result)))))
    (is (= 2 (:dcs/cycle (last (:dcs/trace result)))))))

(deftest pv-overrides-drive-the-simulated-process
  (let [result (runner/dry-run (reactor-system)
                                {:n-cycles 2 :dt 1.0
                                 :initial-values {"PV1" 20.0}
                                 :pv-overrides [nil {"PV1" 95.0}]})
        events-by-cycle (map (comp count :dcs/events) (:dcs/trace result))]
    (is (= [0 1] events-by-cycle))))
