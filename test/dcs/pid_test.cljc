(ns dcs.pid-test
  (:require [clojure.test :refer [deftest is testing]]
            [dcs.pid :as pid]))

(defn- abs* [v] (if (neg? v) (- v) v))

(deftest converges-to-setpoint
  (testing "repeated steps drive pv toward sp against a toy first-order plant"
    (loop [i 0 state (pid/init-state) pv 0.0]
      (if (>= i 200)
        (is (< (abs* (- 100.0 pv)) 1.0))
        (let [{:dcs/keys [output state']} (pid/step {:kp 1.0 :ki 0.5 :kd 0.0}
                                                     state pv 100.0 0.1 [0.0 1000.0])
              pv' (+ pv (* 0.1 (- output pv)))]
          (recur (inc i) state' pv'))))))

(deftest clamps-to-output-limits
  (let [{:dcs/keys [output]} (pid/step {:kp 100.0 :ki 0.0 :kd 0.0} (pid/init-state) 0.0 100.0 1.0 [0.0 10.0])]
    (is (= 10.0 output))))

(deftest anti-windup-freezes-integral-when-saturated
  (let [tuning {:kp 0.0 :ki 10.0 :kd 0.0}
        limits [0.0 10.0]
        s0 (pid/init-state)
        {s1 :dcs/state'} (pid/step tuning s0 0.0 100.0 1.0 limits)
        {s2 :dcs/state'} (pid/step tuning s1 0.0 100.0 1.0 limits)]
    (is (= (:dcs/integral s1) (:dcs/integral s2)))))

(deftest bumpless-transfer-matches-manual-output
  (let [tuning {:kp 2.0 :ki 0.1 :kd 0.0}
        manual-output 42.0
        state (pid/transfer-to-auto tuning 50.0 60.0 manual-output)
        {:dcs/keys [output]} (pid/step tuning state 50.0 60.0 0.0 [0.0 100.0])]
    (is (= manual-output output))))
